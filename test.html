<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8">
  <title>DOMJuice Test Suite</title>

  <!-- Hide templates -->
  <style>.template { display: none }</style>

  <!-- jQuery -->
  <script src="http://code.jquery.com/jquery-latest.js"></script>

  <!-- QUnit -->
  <!-- Prevent auto-start to make it work in combination with Coffee. -->
  <link rel="stylesheet" href="http://code.jquery.com/qunit/git/qunit.css" type="text/css" media="screen">
  <script type="text/javascript" src="http://code.jquery.com/qunit/git/qunit.js"></script>
  <script>QUnit.config.autostart = false;</script>

  <!-- Backbone.js -->
  <script src="http://cdnjs.cloudflare.com/ajax/libs/underscore.js/1.1.6/underscore-min.js"></script>
  <script src="http://cdnjs.cloudflare.com/ajax/libs/backbone.js/0.3.3/backbone-min.js"></script>

  <!-- CoffeeScript -->
  <script src="http://jashkenas.github.com/coffee-script/extras/coffee-script.js"></script>

  <!-- DOMJuice itself -->
  <script src="domjuice.coffee" type="text/coffeescript"></script>
  <script src="domjuice-backbone.coffee" type="text/coffeescript"></script>

  <!-- Test helpers -->
  <script type="text/coffeescript">

    # Set these as globals for easy access.
    window.Model = Backbone.Model
    window.Collection = Backbone.Collection

    # Helper to quickly build a template from an existing element by ID.
    window.template = (id) ->
      element = $("##{id}")[0].cloneNode yes
      $(element).removeClass 'template'
      element.id = ''
      DOMJuice element

    # Helper to quickly run a template from an existing element and a context.
    # Takes the element ID, the context, and returns a jQuery object for the resulting element.
    window.build = (id, context) ->
      klass = template id
      instance = new klass context
      $ instance.el

  </script>
</head>
<body>


  <!-- Tests follow -->

  <div id="basic-template" class="template">
    <p></p>
  </div>
  <script type="text/coffeescript">
    test 'Basic template with DOM input', ->
      res = build 'basic-template'
      ok res.find('p').length is 1, "result should match template content"

    test 'Basic template with string markup input', ->
      template = DOMJuice '<div><p></p></div>'
      instance = new template
      ok $('p', instance.el).length is 1, "result should match template content"
  </script>

  <div id="varattr-template" class="template">
    <p class:="cssClass"></p>
  </div>
  <script type="text/coffeescript">
    test 'Basic attribute operation', 2, ->
      res = build 'varattr-template', cssClass: 'foobar'
      ok res.find('p').hasClass('foobar'), "paragraph tag should have class 'foobar'"

      res = build 'varattr-template'
      ok not res.find('p').hasClass('undefined'), "paragraph tag shouldn't have class 'undefined'"

    test 'Variable attribute', 3, ->
      model = new Model cssClass: 'foobar'

      res = build 'varattr-template', model
      ok res.find('p').hasClass('foobar'), "paragraph tag should have class 'foobar'"

      model.set cssClass: 'baz'
      ok not res.find('p').hasClass('foobar'), "paragraph tag should no longer have class 'foobar'"
      ok res.find('p').hasClass('baz'), "paragraph tag should have class 'baz'"
  </script>

  <div id="varcontent-template" class="template">
    <p content:="text"></p>
  </div>
  <script type="text/coffeescript">
    test 'Basic content operation', 2, ->
      res = build 'varcontent-template', text: 'foobar'
      ok res.find('p').text() is 'foobar', "paragraph should have text foobar"

      res = build 'varcontent-template'
      ok res.find('p').text() is '', "paragraph should be empty"

    test 'Variable content', 2, ->
      model = new Model text: 'foobar'

      res = build 'varcontent-template', model
      ok res.find('p').text() is 'foobar', "paragraph should have content foobar"

      model.set text: false
      ok res.find('p').text() is '', "paragraph should be empty"
  </script>

  <div id="section-template" class="template">
    <ul>
      <li section:='items'></li>
    </ul>
  </div>
  <script type="text/coffeescript">
    test 'Basic section operation', 4, ->
      res = build 'section-template', items: [{}, {}, {}]
      ok res.find('li').length is 3, 'list should have 3 items'

      res = build 'section-template', items: {}
      ok res.find('li').length is 1, 'list should have 1 item'

      res = build 'section-template', items: true
      ok res.find('li').length is 1, 'list should have 1 item'

      res = build 'section-template', items: false
      ok res.find('li').length is 0, 'list should be empty'

    test 'Variable sized section', 4, ->
      collection = new Collection [{}, {}, {}]

      res = build 'section-template', items: collection
      ok res.find('li').length is 3, 'list should have 3 items'

      collection.add {}
      ok res.find('li').length is 4, 'list should have 4 items'

      collection.remove collection.models[1]
      ok res.find('li').length is 3, 'list should have 3 items'

      collection.refresh []
      ok res.find('li').length is 0, 'list should be empty'
  </script>

  <div id="neg-section-template" class="template">
    <p section:="!nono"></p>
  </div>
  <script type="text/coffeescript">
    test 'Negated section operation', 3, ->
      res = build 'neg-section-template', nono: false
      ok res.find('p').length is 1, "result should have a single paragraph"

      res = build 'neg-section-template', nono: {}
      ok res.find('p').length is 0, "result shouldn't have a paragraph"

      res = build 'neg-section-template', nono: [{}, {}]
      ok res.find('p').length is 0, "result shouldn't have a paragraph"

    test 'Variable negated section', 6, ->
      model = new Model nono: false

      res = build 'neg-section-template', model
      ok res.find('p').length is 1, "result should have a single paragraph"

      model.set nono: true
      ok res.find('p').length is 0, "result shouldn't have a paragraph"

      collection = new Collection [{}, {}]

      res = build 'neg-section-template', nono: collection
      ok res.find('p').length is 0, "result shouldn't have a paragraph"

      collection.remove collection.models[0]
      ok res.find('p').length is 0, "result shouldn't have a paragraph"

      collection.remove collection.models[0]
      ok res.find('p').length is 1, "result should have a single paragraph"

      collection.add {}
      ok res.find('p').length is 0, "result shouldn't have a paragraph"
  </script>

  <div id="root-ref-template" class="template">
    <p id="a1" content:="a"></p>
    <p id="b1" content:="b"></p>
    <div section:="level2">
      <p id="a2" content:="a"></p>
      <p id="b2" content:="b"></p>
      <div section:="level3">
        <p id="a3" content:="a"></p>
        <p id="b3" content:="b"></p>
      </div>
    </div>
  </div>
  <script type="text/coffeescript">
    test 'Root context reference', 6, ->
      res = build 'root-ref-template',
        a: 1, b: 2
        level2:
          a: 3
          level3:
            b: 4

      ok res.find('#a1').text() is '1', 'a1 should contain 1'
      ok res.find('#b1').text() is '2', 'b1 should contain 2'
      ok res.find('#a2').text() is '3', 'a2 should contain 3'
      ok res.find('#b2').text() is '2', 'b2 should contain 2'
      ok res.find('#a3').text() is '1', 'a3 should contain 1'
      ok res.find('#b3').text() is '4', 'b3 should contain 4'
  </script>

  <div id="partial-parent-template" class="template">
    <div content:="@child"></div>
  </div>
  <div id="partial-child-template" class="template">
    <p content:="text"></p>
  </div>
  <script type="text/coffeescript">
    test 'Basic partial', 1, ->
      DOMJuice.partials['child'] = template 'partial-child-template'
      res = build 'partial-parent-template', text: 'foobar'
      ok res.find('p').text() is 'foobar', "paragraph should have content 'foobar'"
  </script>

  <div id="content-anim-template" class="template">
    <div content:="text" fx:="test">
      placeholder
    </div>
  </div>
  <script type="text/coffeescript">
    test 'Content animation', 8, ->
      DOMJuice.registerAnimation 'test', class TestAnimator

        add: (elements, options) ->
          ok not @addCalled, '`add` is called once'
          @addCalled = yes

          ok options.content, '`content` option should be set'

          $el = $ elements
          ok $el.length is 1 and $el.text() is 'bar', "`add` should be called with the 'bar' span"

        remove: (elements, options, callback) ->
          ok not @removeCalled, '`remove` is called once'
          @removeCalled = yes

          ok options.content, '`content` option should be set'

          $el = $ elements
          ok $el.length is 1 and $el.text() is 'foo', "`remove` is called with the 'foo' span"

          $div = $el.parent()

          stop 100
          setTimeout ->
            start()

            ok $div.children().length is 2, 'div should have 2 children before callback'
            do callback
            ok $div.children().length is 1, 'div should have 1 child after callback'

          , 0

      model = new Model text: 'foo'
      res = build 'content-anim-template', model
      model.set text: 'bar'
  </script>

  <div id="section-anim-template" class="template">
    <ul>
      <li section:="items" fx:="test" content:="text"></li>
    </ul>
  </div>
  <script type="text/coffeescript">
    test 'Section add animation', 5, ->
      DOMJuice.registerAnimation 'test', class TestAnimator

        add: (elements, options) ->
          ok not @addCalled, '`add` is called once.'
          @addCalled = yes

          $el = $ elements
          ok $el.length is 1 and $el.text() is 'c', "`add` should be called with the 'c' list item"

          $ul = $el.parent()
          ok $ul.children().length is 3, 'list should have 3 items in `add`'

          ok $ul.children().eq(1).text() is 'b', "second list item should be 'b'"
          ok $ul.children().eq(2).text() is 'c', "third list item should be 'c'"

        remove: (elements, options) ->
          ok no, "`remove` shouldn't be called."

      collection = new Collection [{ text: 'a' }, { text: 'b' }]
      res = build 'section-anim-template', items: collection
      collection.add text: 'c'

    test 'Section remove animation', 6, ->
      DOMJuice.registerAnimation 'test', class TestAnimator

        add: (elements, options) ->
          ok no, "`add` shouldn't be called."

        remove: (elements, options, callback) ->
          ok not @removeCalled, '`remove` is called once.'
          @removeCalled = yes

          $el = $ elements
          ok $el.length is 1 and $el.text() is 'b', "`remove` should be called with the 'b' list item"

          $ul = $el.parent()
          ok $ul.children().length is 3, 'list should have 3 items before callback'
          do callback
          ok $ul.children().length is 2, 'list should have 2 items after callback'

          ok $ul.children().eq(0).text() is 'a', "first list item should be 'a'"
          ok $ul.children().eq(1).text() is 'c', "second list item should be 'c'"

      collection = new Collection [{ text: 'a' }, { id: 'foo', text: 'b' }, { text: 'c' }]
      res = build 'section-anim-template', items: collection
      collection.remove 'foo'

    test 'Section refresh animation', 10, ->
      DOMJuice.registerAnimation 'test', class TestAnimator

        add: (elements, options) ->
          ok not @addCalled, '`add` is called once.'
          @addCalled = yes

          ok options.refresh, '`refresh` option should be set'

          $el = $ elements
          ok $el.length is 2, "`add` should be called with 2 list items"

        remove: (elements, options, callback) ->
          ok not @removeCalled, '`remove` is called once.'
          @removeCalled = yes

          ok options.refresh, '`refresh` option should be set'

          $el = $ elements
          ok $el.length is 1 and $el.text() is 'a', "`remove` should be called with the 'a' list item"

          $ul = $el.parent()

          stop 100
          setTimeout ->
            start()

            ok $ul.children().length is 3, 'list should have 3 items before callback'
            do callback
            ok $ul.children().length is 2, 'list should have 2 items after callback'

            ok $ul.children().eq(0).text() is 'b', "first list item should be 'b'"
            ok $ul.children().eq(1).text() is 'c', "second list item should be 'c'"

          , 0

      collection = new Collection [{ text: 'a' }]
      res = build 'section-anim-template', items: collection
      collection.refresh [{ text: 'b' }, { text: 'c' }]
  </script>


  <!-- QUnit boilerplate -->
  <h1 id="qunit-header">DOMJuice Test Suite</h1>
  <h2 id="qunit-banner"></h2>
  <div id="qunit-testrunner-toolbar"></div>
  <h2 id="qunit-userAgent"></h2>
  <ol id="qunit-tests"></ol>
  <div id="qunit-fixture"></div>
  <script type="text/coffeescript">QUnit.start()</script>


</body>
</html>
